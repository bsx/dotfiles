# bsx prompt theme

prompt_bsx_help () {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt bsx [<promptcolor> [<usercolor> [<rootcolor>]]]

EOF
}

prompt_bsx_setup () {
  prompt_bsx_prompt=${1:-'blue'}
  prompt_bsx_user=${2:-'green'}
  prompt_bsx_root=${3:-'red'}

  if [ "$USER" = 'root' ]
  then
    base_prompt="%B%F{$prompt_bsx_root}%m%k%1v "
  else
    base_prompt="%B%F{$prompt_bsx_user}%n@%m%k%1v "
  fi
  post_prompt="%b%f%k"

  # setopt noxtrace localoptions

  path_prompt="%B%F{$prompt_bsx_prompt}%1~"
  PS1="%(?..%B%F{red}%?%k - )$base_prompt$path_prompt [%h]%# $post_prompt"
  PS2="$base_prompt$path_prompt %_> $post_prompt"
  PS3="$base_prompt$path_prompt ?# $post_prompt"
}

prompt_bsx_setup "$@"
